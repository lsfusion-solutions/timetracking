MODULE Customer;

REQUIRE MasterData, Partner;

CLASS Customer 'Клиент';
TABLE customer(Customer);

name 'Наименование' = DATA ISTRING[200] (Customer) CHARWIDTH 15;
inactive 'Неактивный' = DATA BOOLEAN (Customer);
active 'Активный' (Customer c) = c IS Customer AND NOT inactive(c);

partner 'Контрагент' = DATA Partner (Customer);
namePartner 'Контрагент' (Customer c) = namePartner(partner(c));

requirePartner 'Контрагент для каждого клиента' = DATA BOOLEAN ();

CONSTRAINT requirePartner() AND NOT partner(Customer c) AND c IS Customer MESSAGE 'Не задан контрагент для клиента';

EXTEND FORM options PROPERTIES requirePartner();

DESIGN options {
    commons {
        MOVE PROPERTY (requirePartner());
    }    
}

//customer 'Клиент' (Partner p) = GROUP AGGR Customer c BY partner(c);
customer 'Клиент' (Partner p) = GROUP LAST Customer c ORDER c BY partner(c);

inn 'ИНН' = DATA ISTRING (Customer) CHARWIDTH 20;
customer (ISTRING inn) = GROUP LAST Customer c ORDER c BY inn(c);

ogrn 'ОГРН' = DATA ISTRING (Customer) CHARWIDTH 20;

CONSTRAINT SET(Customer c IS Customer) AND NOT (inn(c) OR ogrn(c)) 
    MESSAGE 'Необходимо указать ИНН или ОГРН';

FORM customer 'Клиент'
    OBJECTS c = Customer PANEL
    PROPERTIES(c) name, namePartner, inactive, inn, ogrn

    EDIT Customer OBJECT c
;

DESIGN customer {
    NEW pane FIRST {
        fill = 1;
        type = SPLITH;
        NEW leftPane {
            fill = 1;
            MOVE BOX(c) {  
                NEW lane1 {
                    type = CONTAINERH;
                    MOVE PROPERTY(name(c));
                    MOVE PROPERTY(namePartner(c));
                    MOVE PROPERTY(inactive(c));
                }
                NEW lane2 {
                    type = CONTAINERH;
                    caption = 'Для иностранных компаний укажите "0"';
                    MOVE PROPERTY(inn(c));
                    MOVE PROPERTY(ogrn(c));
                }
            }
        }
        NEW tabPane {
            fill = 2;
            tabbed = TRUE;
        }
    }
}

FORM customers 'Клиенты'
    OBJECTS c = Customer
    PROPERTIES(c) READONLY active, name, namePartner
    ORDERS name(c)
    
    FILTERGROUP active FILTER 'Активный' active(c) 'F5' DEFAULT
    
    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE 
;

FORM dialogCustomer 'Выбор клиента'
    OBJECTS c = Customer
    PROPERTIES(c) READONLY active, name
    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE 
    ORDERS name(c)
    
    FILTERGROUP active FILTER 'Активный' active(c) 'F5' DEFAULT
    
    LIST Customer OBJECT c
;

NAVIGATOR {
    masterData {
        NEW customers;
    }
}