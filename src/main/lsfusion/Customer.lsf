MODULE Customer;

REQUIRE MasterData;

CLASS Customer 'Клиент';
TABLE customer(Customer);

name 'Наименование' = DATA ISTRING[200] (Customer) CHARWIDTH 15;
inactive 'Неактивный' = DATA BOOLEAN (Customer);
active 'Активный' (Customer c) = c IS Customer AND NOT inactive(c);

FORM customer 'Клиент'
    OBJECTS c = Customer PANEL
    PROPERTIES(c) name, inactive

    EDIT Customer OBJECT c
;

DESIGN customer {
    NEW pane FIRST {
        fill = 1;
        type = SPLITH;
        NEW leftPane {
            fill = 1;
            MOVE BOX(c) {  
                NEW lane1 {
                    type = CONTAINERH;
                    MOVE PROPERTY(name(c));
                    MOVE PROPERTY(inactive(c));
                }
            }
        }
        NEW tabPane {
            fill = 2;
            type = TABBED;
        }
    }
}

FORM customers 'Клиенты'
    OBJECTS c = Customer
    PROPERTIES(c) READONLY active, name
    ORDERS name(c)
    
    FILTERGROUP active FILTER 'Активный' active(c) 'F5' DEFAULT
    
    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE 
;

FORM dialogCustomer 'Выбор клиента'
    OBJECTS c = Customer
    PROPERTIES(c) READONLY active, name
    ORDERS name(c)
    
    FILTERGROUP active FILTER 'Активный' active(c) 'F5' DEFAULT
    
    LIST Customer OBJECT c
;

NAVIGATOR {
    masterData {
        NEW customers;
    }
}

CLASS CounterpartyType 'Тип контрагента' {
    person 'Физическое лицо',
    legalEntity 'Юридическое лицо'
}

name 'Тип' (CounterpartyType t) = staticCaption(t);

type = DATA CounterpartyType (Customer);
nameType 'Тип контрагента' (Customer c) = name(type(c));

FORM counterpartyType 'Тип контрагента'
    OBJECTS t = CounterpartyType
    PROPERTIES(t) READONLY name
    
    LIST CounterpartyType OBJECT t
;

EXTEND FORM customer
    PROPERTIES nameType(c)
;

DESIGN customer {
    BOX(c) {
        NEW lane2 {
            type = CONTAINERH;
            MOVE PROPERTY(nameType(c));
        }
    }
}