MODULE Task;

REQUIRE Project, Employee, Email, Category;

CLASS Task 'Задача';
TABLE task(Task);

name 'Название' = DATA STRING[100] (Task) CHARWIDTH 20;

description 'Описание' = DATA TEXT (Task);

project 'Проект' = DATA Project (Task);
nameProject 'Проект' (Task e) = name(project(e)); 

CONSTRAINT DROPPED (Project p IS Project) AND [GROUP SUM 1 BY PREV (project(Task t))](p) MESSAGE 'Нельзя удалить проект, по которому есть задачи';

customer 'Клиент' (Task e) = customer(project(e));
nameCustomerProject 'Клиент' (Task e) = name(customer(e));

employee 'Сотрудник' = DATA Employee (Task) NONULL;
nameEmployee 'Сотрудник' (Task e) = name(employee(e)) CHARWIDTH 20;

employee(Task t) <- currentEmployee() WHEN SET(t IS Task);  

CONSTRAINT (CHANGED(employee(Task e)) OR CHANGED(project(e))) AND employee(e) AND project(e) AND NOT assigned(project(e), employee(e))
    CHECKED BY employee, project[Task]
    MESSAGE 'Для трудозатрат задан сотрудник, не назначенный на проект';

completed 'Завершена' = DATA BOOLEAN (Task);
notCompleted 'Не завершена' (Task t) = t IS Task AND NOT completed(t);

// Тип
CLASS TaskType 'Тип задачи';
TABLE taskType (TaskType);

name 'Название' = DATA STRING[50] (TaskType);

in 'Вкл.' = DATA BOOLEAN (TaskType, Practice) CHARWIDTH 3;
inPracticGroup (TaskType t, Practice pc) = GROUP MAX level(pc, Practice pp) IF in(t, pp);

existInChild() = GROUP SUM 1 IF parent(Practice pc) AND in(TaskType t, pc); 

onStarted() + {
    IF existInChild() THEN NEWSESSION {
        useTreePractics() <- TRUE;
        APPLY;
    }
}

CONSTRAINT DROPPED(useTreePractics()) AND existInChild()
    MESSAGE 'Невозможно отключить использование дерева практик пока есть типы задач для подчиненных практик'; 

practics 'Практики' (TaskType t) = GROUP CONCAT name(Practice p) IF in(t,p), ', ' CHARWIDTH 30;

FORM taskTypes 'Типы задач'
    OBJECTS a = TaskType
    PROPERTIES (a) name, practics READONLY, NEW, DELETE
    
    OBJECTS p 'Список' = Practice
    PROPERTIES(p) READONLY name
    PROPERTIES(p) READONLY SHOWIF useTreePractics() nameParent, canonicalName
    PROPERTIES in(a, p)
    FILTERS NOT parent(p) OR useTreePractics()
    
    TREE groups g 'Дерево' = Practice PARENT parent(g)
    PROPERTIES READONLY name(g)
    PROPERTIES in(a, g)
    
    EVENTS ON CHANGE g { SEEK taskTypes.p = g; }
;

DESIGN taskTypes {
    OBJECTS {
        NEW tabbedPane {
            type = TABBED;
            fill = 1;
            caption = 'Практики';
            MOVE BOX (TREE groups) { showIf = useTreePractics(); }
            MOVE BOX (p);
        }
    }
}

FORM dialogTaskType 'Выбор типа задачи'
    OBJECTS p = Practice
    PROPERTIES (p) name

    FILTERS NOT parent(p) OR useTreePractics()
    
    TREE groups g = Practice PARENT parent(g)
    PROPERTIES (g) name
    
    EVENTS ON CHANGE g { SEEK dialogTaskType.p = g; }

    OBJECTS a = TaskType
    PROPERTIES(a) READONLY name
    FILTERGROUP inPractice
        FILTER 'В практике' inPracticGroup(a, p) DEFAULT
    
    LIST TaskType OBJECT a;
;

DESIGN dialogTaskType {
    OBJECTS {
        type = CONTAINERH;
        MOVE BOX (p) { showIf = NOT useTreePractics(); }
        MOVE BOX (TREE groups) { showIf = useTreePractics(); }
        MOVE BOX (a);
    }
    
}

NAVIGATOR {
    masterData {
        NEW taskTypes;
    }
}

taskType 'Тип задачи' = DATA TaskType (Task) NONULL;
nameTaskType 'Тип задачи' (Task t) = name(taskType(t));

// --- Редактирование задачи

FORM task 'Задача'
    OBJECTS t = Task PANEL
    PROPERTIES(t) nameCustomerProject, nameProject, nameTaskType, name, description
    ORDERS nameCustomerProject(t)
    
    EDIT Task OBJECT t
;

DESIGN task {
    size = (1024, 768);
    OBJECTS {
        NEW projectInfo FIRST {
            caption = 'Проект';
            type = CONTAINERH;
            MOVE PROPERTY(nameCustomerProject(t));
            MOVE PROPERTY(nameProject(t));
            MOVE PROPERTY(nameTaskType(t));
            MOVE PROPERTY(name(t));
        }
        
        NEW projectDescr {
            fill = 1;
            type = SPLITH;
            MOVE PROPERTY(description(t)) {
                fill = 1;
                panelCaptionAbove = TRUE;
            }
        }
        
    }
}

read 'Обработан' = DATA BOOLEAN (Email);

EXTEND FORM mail PROPERTIES READONLY read(e);

id 'Код' = DATA ISTRING[15](Project);
project = GROUP AGGR Project p BY id(p);

EXTEND FORM customer PROPERTIES id(p);

emailTaskType = DATA TaskType();
nameEmailTaskType 'Тип задач, созданных по email' () = name(emailTaskType());

EXTEND FORM options PROPERTIES nameEmailTaskType();
DESIGN options { commons { MOVE PROPERTY (nameEmailTaskType()); } }

category = DATA Category(Task) INDEXED;
nameCategory 'Категория для задачи' (Task t) = name(category(t));

WHEN LOCAL (CHANGED (employee(Task t)) OR CHANGED (project(t))) AND NOT CHANGED (category(t)) DO {
    category(t) <- OVERRIDE category(employee(t), project(t)), category(employee(t));
}

onStarted() + {
    FOR Task t IS Task AND NOT category(t) DO {
        category(t) <- OVERRIDE category(employee(t), project(t)), category(employee(t));
    }
}