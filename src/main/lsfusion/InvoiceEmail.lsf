MODULE InvoiceEmail;

REQUIRE Invoice, TimeEntryWord, LegalEntity;

NAMESPACE Invoice;

checkHoursLimit 'Проверять приближение к лимиту часов' = DATA BOOLEAN ();

EXTEND FORM options PROPERTIES checkHoursLimit();
DESIGN options {
    commons {
        MOVE PROPERTY (checkHoursLimit());
    }
}

sendLimitEmails 'Получать уведомления о приближении к лимиту часов' = DATA BOOLEAN (Employee, Project);
sendLimitEmails 'Получать уведомления о приближении к лимиту часов' = DATA BOOLEAN (Person, Project);

EXTEND FORM customer PROPERTIES sendLimitEmails(e, p) AFTER name(e);
EXTEND FORM customer PROPERTIES sendLimitEmails(r, p) BEFORE lastName(r);

hoursPay 'Часов к оплате' (Project p, Category c) = hours(p, c, extractMonth(currentDate()), currentYear()) (-) hoursMonthly(p, c);
sumPay 'Сумма к оплате' (Project p, Category c) = hoursPay(p, c) * rate(p, c);
sumPay 'Сумма к оплате' (Project p) = GROUP SUM sumPay(p, Category c);

sent90 = DATA BOOLEAN (Project, Month, INTEGER);
sent95 = DATA BOOLEAN (Project, Month, INTEGER);
sent100 = DATA BOOLEAN (Project, Month, INTEGER);

sumTotal 'Сумма (всего)' (Project p) = sumMonthly(p) (+) sumPay(p);
percentHours 'Израсходовано часов, %' (Project p, Category c, DATE d) = round0(hours(p, c, extractMonth(d), extractYear(d))/hoursMonthly(p, c) * 100);

FORM messageTableCategory 'Таблица оплаты по категориям'
    OBJECTS pcd = (p = Project, c = Category, d = DATE) PANEL 
    PROPERTIES READONLY sumMonthly = (OVERRIDE formatPrice(sumMonthly(p)) + ' ₽', '0,00 ₽'), 
                        sumPay = (OVERRIDE formatPrice(sumPay(p)) + ' ₽', '0,00 ₽'), 
                        sumTotal = (OVERRIDE formatPrice(sumTotal(p)) + ' ₽', '0,00 ₽'), nameCustomer(p)
    PROPERTIES READONLY name(c), percentHours(p, c, d)
    
    OBJECTS c1 = Category
    PROPERTIES (c1) READONLY name
    PROPERTIES (p, c1) READONLY hoursMonthly
    FILTERS hoursMonthly(p, c1)
    ORDERS name(c1)
    
    OBJECTS c2 = Category
    PROPERTIES (c2) READONLY name
    PROPERTIES READONLY hoursPay(p, c2), 
                        rate = (OVERRIDE formatPrice(rate(p, c2)) + ' ₽', '0,00 ₽'), 
                        sumPay2 = (OVERRIDE formatPrice(sumPay(p, c2)) + ' ₽', '0,00 ₽')
    FILTERS hoursPay(p, c2) > 0 OR sumPay(p, c2) > 0
    ORDERS name(c2)
;

sendEmailLimitHoursProjects 'Разослать письма о приближении к лимиту по проекту' () {
    
    LOCAL email = TEXT ();
    LOCAL toSend = BOOLEAN (Project);
    
    toSend (Project p) <- (NOT sent90(p, extractMonth(currentDate()), currentYear()) AND round2(hours(p, extractMonth(currentDate()), currentYear())/hoursMonthly(p)) >= 0.90) OR 
                          (NOT sent95(p, extractMonth(currentDate()), currentYear()) AND round2(hours(p, extractMonth(currentDate()), currentYear())/hoursMonthly(p)) >= 0.95) OR 
                          (NOT sent100(p, extractMonth(currentDate()), currentYear()) AND round2(hours(p, extractMonth(currentDate()), currentYear())/hoursMonthly(p)) >= 1.0);
    
    FOR toSend(Project p) DO {
        email() <- CONCAT ';', (GROUP CONCAT email(Employee e) IF assigned(p, e) AND sendLimitEmails(e, p), ';'), 
                               (GROUP CONCAT email(Person r) IF sendLimitEmails(r, p), ';');
        IF email() THEN EMAIL 
            SUBJECT CONCAT ' ', nameCustomer(p) + ':', 'израсходовано ' + 
                round0(hours(p,  extractMonth(currentDate()), currentYear())/hoursMonthly(p) * 100) + '% времени, включённого в оплату'
            TO email()
            BODY 'Здравствуйте!<br><br>Обратите внимание что у клиента ' + nameCustomer(p) + ' израсходовано ' + 
                round0(hours(p, extractMonth(currentDate()), currentYear())/hoursMonthly(p) * 100) + 
                '% времени, включённого в оплату в текущем периоде.<br><br>С уважением,<br><br>Ваша CRM система!';
    }
    sent90(Project p, Month m, INTEGER y) <- round2(hours(p, extractMonth(currentDate()), currentYear())/hoursMonthly(p)) >= 0.90 WHERE m = extractMonth(currentDate()) AND y = currentYear();
    sent95(Project p, Month m, INTEGER y) <- round2(hours(p, extractMonth(currentDate()), currentYear())/hoursMonthly(p)) >= 0.95 WHERE m = extractMonth(currentDate()) AND y = currentYear();
    sent100(Project p, Month m, INTEGER y) <- round2(hours(p, extractMonth(currentDate()), currentYear())/hoursMonthly(p)) >= 1.0 WHERE m = extractMonth(currentDate()) AND y = currentYear();
    APPLY;
}

sent90 = DATA BOOLEAN (Project, Category, Month, INTEGER);
sent95 = DATA BOOLEAN (Project, Category, Month, INTEGER);
sent100 = DATA BOOLEAN (Project, Category, Month, INTEGER);

sendEmailLimitHoursCategories 'Разослать письма о приближении к лимиту по категории' () {
    
    LOCAL email = TEXT ();
    LOCAL toSend = BOOLEAN (Project, Category);
    
    toSend (Project p, Category c) <- (NOT sent90(p, c, extractMonth(currentDate()), currentYear()) AND round2(hours(p, c, extractMonth(currentDate()), currentYear())/hoursMonthly(p, c)) >= 0.90) OR 
                                      (NOT sent95(p, c, extractMonth(currentDate()), currentYear()) AND round2(hours(p, c, extractMonth(currentDate()), currentYear())/hoursMonthly(p, c)) >= 0.95) OR 
                                      (NOT sent100(p, c, extractMonth(currentDate()), currentYear()) AND round2(hours(p, c, extractMonth(currentDate()), currentYear())/hoursMonthly(p, c)) >= 1.0);
        
    FOR toSend(Project p, Category c) DO {
        
        LOCAL bodyFile = FILE ();
        PRINT messageTableCategory OBJECTS p = p, c = c, d = currentDate() HTML TO bodyFile;
        
        email() <- CONCAT ';', (GROUP CONCAT email(Employee e) IF assigned(p, e) AND sendLimitEmails(e, p), ';'), 
                               (GROUP CONCAT email(Person r) IF sendLimitEmails(r, p), ';');
        IF email() THEN EMAIL 
            SUBJECT CONCAT ' ', nameCustomer(p) + ':', name(c) + ':', 'израсходовано ' + 
                round0(hours(p,  c, extractMonth(currentDate()), currentYear())/hoursMonthly(p, c) * 100) + '% времени, включённого в оплату'
            TO email()
            BODY bodyFile();
    }
    sent90(Project p, Category c, Month m, INTEGER y) <- round2(hours(p, c, extractMonth(currentDate()), currentYear())/hoursMonthly(p, c)) >= 0.90 WHERE m = extractMonth(currentDate()) AND y = currentYear(); 
    sent95(Project p, Category c, Month m, INTEGER y) <- round2(hours(p, c, extractMonth(currentDate()), currentYear())/hoursMonthly(p, c)) >= 0.95 WHERE m = extractMonth(currentDate()) AND y = currentYear();
    sent100(Project p, Category c, Month m, INTEGER y) <- round2(hours(p, c, extractMonth(currentDate()), currentYear())/hoursMonthly(p, c)) >= 1.0 WHERE m = extractMonth(currentDate()) AND y = currentYear();
    APPLY;
}

sendEmailLimitHours 'Разослать письма о приближении к лимиту' () {
    sendEmailLimitHoursProjects();
    sendEmailLimitHoursCategories();
}