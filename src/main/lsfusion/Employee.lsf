MODULE Employee;

REQUIRE Practice, MasterData, Authentication, Security, Utils;

CLASS Employee 'Сотрудник' : CustomUser;
TABLE employee (Employee);

code 'Код' = DATA STRING[10] (Employee);
fired 'Уволен' = DATA BOOLEAN (Employee);

FORM employee 'Сотрудник'
    OBJECTS e = Employee PANEL
    PROPERTIES(e) firstName, lastName, login, sha256Password ON CHANGE changeSHA256Password(e), email, nameMainRole, isLocked, fired

    OBJECTS r = UserRole
    PROPERTIES(r) READONLY name, sid
    PROPERTIES(e, r) in

    EDIT Employee OBJECT e
;

DESIGN employee {
    OBJECTS {
        NEW tab AFTER BOX (e) {
            type = TABBED;
            fill = 1;
            MOVE BOX(r) { caption = 'Роли'; }
        }
    }
}

FORM employees 'Сотрудники'
    OBJECTS e = Employee
    PROPERTIES(e) READONLYIF isReadonly() firstName, lastName, code, login, email, nameMainRole, isLocked
    
    PROPERTIES(e) NEWSESSION NEW, EDIT, DELETE 
    FILTERGROUP fired FILTER 'Не уволены' NOT fired(e) DEFAULT 
;
@extendFormEditable(employees);

FORM dialogEmployee 'Выбор сотрудника'
    OBJECTS e = Employee
    PROPERTIES(e) READONLY firstName, lastName, code, isLocked
    FILTERS NOT fired(e)
    
    LIST Employee OBJECT e
;

NAVIGATOR {
    masterData {
        NEW employees;
    }
}

currentEmployee = currentUser() AS Employee;


practice 'Практика' = DATA Practice (Employee);
namePractice 'Практика' (Employee e) = name(practice(e));

EXTEND FORM employees
    PROPERTIES(e) READONLYIF isReadonly() namePractice
;

EXTEND FORM employee
    PROPERTIES(e) namePractice
;

managers 'Руководители' = DATA BOOLEAN (Employee, Employee);

nameManagers 'Руководители' (Employee e) = 
    GROUP CONCAT (CONCAT ' ',firstName(Employee manager), lastName(manager)), ', ' IF managers(e, manager) CHARWIDTH 40;
nameEmployees 'Подчиненные' (Employee e) = 
    GROUP CONCAT (CONCAT ' ',firstName(Employee employee), lastName(employee)), ', ' IF managers(employee, e) CHARWIDTH 40;
    
WHEN SET(fired(Employee e)) DO {
    managers(e, Employee managr) <- NULL;
    managers(Employee employee, e) <- NULL;
}
    
include 'Вкл' = DATA LOCAL BOOLEAN (Employee);

EXTEND FORM dialogEmployee
    PROPERTIES(e) include BEFORE firstName(e)
;

selectManagers (Employee e) {
    include(Employee manager) <- managers(e, manager);
    DIALOG dialogEmployee DO {      
        managers(e, Employee manager) <- include(manager);
    }
}

selectEmployees (Employee e) {
    include(Employee employee) <- managers(employee, e);
    DIALOG dialogEmployee DO {      
        managers(Employee emloyee, e) <- include(emloyee);
    }
}
    
EXTEND FORM employee
    PROPERTIES nameManagers(e) ON CHANGE selectManagers(e), nameEmployees(e) ON CHANGE selectEmployees(e)
;