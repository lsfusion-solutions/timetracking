MODULE Practice;

REQUIRE MasterData;

NAMESPACE Employee;

CLASS Practice 'Практика';
TABLE practice (Practice);

useTreePractics 'Использовать дерево практик' = DATA BOOLEAN ();

EXTEND FORM options PROPERTIES useTreePractics();

DESIGN options {
    commons {
        MOVE PROPERTY (useTreePractics());
    }
}

name 'Наименование' = DATA STRING[50] (Practice) IN id CHARWIDTH 25;

parent 'Родитель' = DATA Practice (Practice);
nameParent 'Родитель' (Practice p) = name(parent(p)) CHARWIDTH 25; 

level 'Уровень' (Practice c, Practice p) =
   RECURSION 1l IF c IS Practice AND p = c
        STEP 2l IF p = parent($p) MATERIALIZED;

canonicalName 'Каноническое имя' (Practice pr) =
   GROUP CONCAT name(Practice p), ' / ' ORDER DESC level(pr, p) CHARWIDTH 50;

FORM practice 'Практика'
    OBJECTS p = Practice PANEL
    PROPERTIES(p) name//, nameParent SHOWIF useTreePractics()
    
    EDIT Practice OBJECT p
;

newPractice 'Добавить' (Practice p) {
    NEW pp = Practice {
        parent(pp) <- p;
        SHOW practice OBJECTS p = pp DOCKED;
    }
    APPLY;
} IMAGE 'add.png';

deleteParent 'Удалить' (Practice p) {
    FOR parent(Practice pp) == p DO { DELETE(pp); }
    DELETE(p);
    APPLY;
} IMAGE 'delete.png';

FORM practices 'Практики'
    OBJECTS p 'Родитель' = Practice 
    PROPERTIES(p) READONLY name
    PROPERTIES(p) NEWSESSION NEW, EDIT, deleteParent
    FILTERS NOT parent(p)

    OBJECTS g 'Практики' = Practice
    PROPERTIES(g) READONLY name, nameParent, canonicalName 
    PROPERTIES NEWSESSION newPractice(p) DRAW g, EDIT(g), DELETE(g)
    FILTERS parent(g) == p
    //EVENTS ON CHANGE g { SEEK practices.p = g; } 
;

DESIGN practices {
    TOOLBARRIGHT(g) {
        MOVE PROPERTY(newPractice(p));
    }
}

DESIGN practices {
    OBJECTS {
        type = SPLITH;
        MOVE BOX (p) { 
            fill = 1; 
        }
        MOVE BOX(g){ 
            fill = 4; 
        }
    }
}

FORM dialogPractices 'Выбор практики'
    OBJECTS p 'Список' = Practice 
    PROPERTIES(p) READONLY name
    FILTERS NOT parent(p) OR useTreePractics()

    OBJECTS g 'Дерево' = Practice
    PROPERTIES(g) READONLY name, nameParent, canonicalName 
    PROPERTIES(g) NEWSESSION NEW, EDIT, DELETE
    FILTERS parent(g)
    //EVENTS ON CHANGE g { SEEK dialogPractices.p = g; }
        
    LIST Practice OBJECT p
;

DESIGN dialogPractices {
    OBJECTS {
        type = CONTAINERH;
        MOVE BOX (g) { showIf = useTreePractics(); }
        MOVE BOX(p){ showIf = NOT useTreePractics(); }
    }
}

NAVIGATOR {
    masterData {
        NEW practices;
    }
}