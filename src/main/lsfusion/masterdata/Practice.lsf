MODULE Practice;

REQUIRE MasterData;

NAMESPACE Employee;

CLASS Practice 'Практика';
TABLE practice (Practice);

name 'Наименование' = DATA STRING[50] (Practice) IN id CHARWIDTH 25;

parent 'Родитель' = DATA Practice (Practice);
nameParent 'Родитель' (Practice p) = name(parent(p)) CHARWIDTH 25; 

level 'Уровень' (Practice c, Practice p) =
   RECURSION 1l IF c IS Practice AND p = c
        STEP 2l IF p = parent($p) MATERIALIZED;

canonicalName 'Каноническое имя' (Practice pr) =
   GROUP CONCAT name(Practice p), ' / ' ORDER DESC level(pr, p) CHARWIDTH 50;

multipleBilling 'Множественный биллинг' = DATA BOOLEAN (Practice);
overMultipleBilling 'Множественный биллинг' (Practice p) = TRUE IF
    GROUP SUM 1 IF multipleBilling(Practice parent) AND level(p, parent);

FORM practice 'Практика'
    OBJECTS p = Practice PANEL
    PROPERTIES(p) name, nameParent, multipleBilling
    
    EDIT Practice OBJECT p
;

newPractice 'Добавить' (Practice p) {
    NEW pp = Practice {
        parent(pp) <- p;
        SHOW practice OBJECTS p = pp DOCKED;
    }
    APPLY;
} IMAGE 'add.png';

deleteParent 'Удалить' (Practice p) {
    FOR parent(Practice pp) == p DO { DELETE(pp); }
    DELETE(p);
    APPLY;
} IMAGE 'delete.png';

FORM practices 'Практики'
    OBJECTS p 'Родитель' = Practice 
    PROPERTIES(p) READONLY name, overMultipleBilling
    PROPERTIES(p) NEWSESSION NEW, EDIT, deleteParent
    FILTERS NOT parent(p)

    OBJECTS g 'Практики' = Practice
    PROPERTIES(g) READONLY name, nameParent, canonicalName, overMultipleBilling
    PROPERTIES NEWSESSION newPractice(p) DRAW g, EDIT(g), DELETE(g)
    FILTERS parent(g) == p
    //EVENTS ON CHANGE g { SEEK practices.p = g; } 
;

DESIGN practices {
    TOOLBARRIGHT(g) {
        MOVE PROPERTY(newPractice(p));
    }
}

DESIGN practices {
    OBJECTS {
        type = SPLITH;
        MOVE BOX (p) { 
            fill = 1; 
        }
        MOVE BOX(g){ 
            fill = 4; 
        }
    }
}

FORM dialogPractices 'Выбор практики'
    OBJECTS g 'Практики' = Practice
    PROPERTIES(g) READONLY name, nameParent, canonicalName 
    PROPERTIES(g) NEWSESSION NEW, EDIT, DELETE
    
    FILTERGROUP parent 
        FILTER 'Детальные' parent(g) DEFAULT
        FILTER 'Общие' NOT parent(g)  
        
    LIST Practice OBJECT g
;

DESIGN dialogPractices {
    OBJECTS {
        type = CONTAINERH;
        MOVE BOX(g);
    }
}

NAVIGATOR {
    masterData {
        NEW practices;
    }
}