MODULE Currency;


REQUIRE SystemEvents, Time, MasterData, DefaultData, System, Utils;

CLASS Currency 'Валюта';
TABLE currency(Currency);

name 'Наименование' = DATA ISTRING[50](Currency);
shortName 'Наименование (сокр.)' = DATA BPSTRING[3] (Currency) CHARWIDTH 4;
reportName 'Наименование (для отчета)' = DATA STRING[25] (Currency) CHARWIDTH 8;

currencyShortName (string) = GROUP AGGR Currency currency WHERE currency IS Currency BY shortName(currency);

//defaultCurrency = DATA Currency (); 
//nameDefaultCurrency 'Валюта по умолчанию' = name(defaultCurrency()) CHARWIDTH 30;

// Формы

FORM currency 'Валюта'
    OBJECTS c = Currency PANEL
    PROPERTIES (c)  name, shortName, reportName

    EDIT Currency OBJECT c
;

FORM currencies 'Валюты'
    OBJECTS c = Currency
    PROPERTIES(c) READONLY name, shortName, reportName
    ORDERS name(c)    

    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE 
;

FORM dialogCurrencies 'Валюты'
    OBJECTS c = Currency
    PROPERTIES(c) READONLY name, shortName
    ORDERS name(c)

    LIST Currency OBJECT c
;

//EXTEND FORM options
//    PROPERTIES() nameDefaultCurrency
//;
//
//DESIGN options {
//    commons {
//        MOVE PROPERTY(nameDefaultCurrency());
//    }
//}

// -------------------------------------------- Курсы валют --------------------------------- //

CLASS TypeExchange 'Тип обмена';
TABLE typeExchange(TypeExchange);
TABLE rateExchange (TypeExchange, Currency, DATE);

name 'Наименование' = DATA ISTRING[50](TypeExchange) IN id;

currency 'Валюта типа обмена'= DATA Currency (TypeExchange);
nameCurrency 'Валюта типа обмена' (TypeExchange typeExchange) = name(currency(typeExchange)) IN base;

FORM typeExchange 'Тип обмена'
    OBJECTS t = TypeExchange PANEL
    PROPERTIES(t) name, nameCurrency
    
    EDIT TypeExchange OBJECT t
;

FORM dialogTypeExchange 'Типы обмена'
    OBJECTS t = TypeExchange PANEL
    PROPERTIES(t) READONLY name, nameCurrency
    
    LIST TypeExchange OBJECT t
;
// Расчет курсов на дату

rate 'Курс обмена' = DATA NUMERIC[20,13] (TypeExchange, Currency, DATE);
typeExchange (name) = GROUP AGGR TypeExchange typeExchange WHERE typeExchange IS TypeExchange BY name(typeExchange);

rateOn 'Курс обмена' (TypeExchange typeExchange, Currency currency, DATE date) =
    GROUP
          LAST rate(typeExchange, currency, DATE dateIn)
          ORDER dateIn
          WHERE rate(typeExchange, currency, dateIn) AND dateIn <= date COMPLEX;

curRate 'Текущий курс обмена' (TypeExchange typeExchange, Currency currency) =
    rateOn(typeExchange, currency, currentDate());

// -------------------------------------------- Курсы валют --------------------------------- //

FORM dialogTypeExchangeCurrency 'Курс обмена на дату'
    OBJECTS t=TypeExchange PANEL
    OBJECTS c=Currency PANEL
    OBJECTS d=DATE PANEL
    PROPERTIES READONLY name(t),  nameCurrency(t), name(c)
    PROPERTIES val=VALUE(d), rate(t,c,d)

;
DESIGN dialogTypeExchangeCurrency {
    BOX {
        MOVE BOX(t) {
            MOVE PROPERTY(name(t)) { focusable = FALSE; charWidth = 40;}
        }
        MOVE BOX(c){
            MOVE BOX(d) {
                horizontal = TRUE;

                MOVE PROPERTY(rate(t,c,d));
            };
            NEW row {
                caption = 'Валюты обмена';
                horizontal = TRUE;
                MOVE PROPERTY(name(c)) { caption = 'Валюта (из)'; charWidth = 20;}
                MOVE PROPERTY(nameCurrency(t)) { caption = 'Валюта (в)'; charWidth = 20;}
            }
        }
    }
    MOVE TOOLBARBOX;
}

dialog 'Добавить' (TypeExchange typeExchange, Currency currency)  { SHOW dialogTypeExchangeCurrency OBJECTS t = typeExchange, c = currency; }  TOOLBAR;
delete 'Удалить' (TypeExchange typeExchange, Currency currency, DATE date)  { rate(typeExchange, currency, date) <- NULL; } IMAGE 'delete.png';

FORM typeExchangeCurrencyDate 'Курсы валют'

    OBJECTS t = TypeExchange
    PROPERTIES(t) READONLY name, nameCurrency
    PROPERTIES(t) NEWSESSION NEW, EDIT, DELETE 
    ORDERS nameCurrency(t)

    OBJECTS c = Currency
    PROPERTIES(c) READONLY name
    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE 
    PROPERTIES(t, c) READONLY curRate
    ORDERS name(c) 

    FILTERGROUP inactive FILTER 'Есть курс' curRate(t, c) 'F11' DEFAULT    

    OBJECTS d = DATE LAST
    PROPERTIES(d) READONLY VALUE

    PROPERTIES(t, c, d) rate
    PROPERTIES(t, c) dialog DRAW d 
    PROPERTIES(t, c, d) delete GRID

    FILTERS rate(t, c, d)

    LIST TypeExchange OBJECT t

;

DESIGN typeExchangeCurrencyDate {
    BOX {
        NEW split {
            fill = 1;
            MOVE BOX(t);
            NEW topContainer {
                fill = 2;
                tabbed = TRUE;
                NEW currentExchangeRates {
                    caption = 'Текущие курсы';
                    horizontal = TRUE;
                    MOVE BOX(c);
                    MOVE BOX(d);
                }
                NEW importExchangeRates {
                    caption = 'Импорт курсов';
                }
            }
        }
        MOVE TOOLBARBOX;
    }
}

NAVIGATOR {
    masterData {
        NEW FOLDER currencyNavigator 'Валюты и курсы' {
            NEW currencies;
            NEW typeExchangeCurrencyDate;
        }
    }
}

extraSID 'Код ЦБРФ' = DATA BPSTRING[6] (Currency);
currencyExtraSID (string) = GROUP AGGR Currency currency WHERE currency IS Currency BY extraSID(currency);

EXTEND FORM currency PROPERTIES (c) extraSID;

EXTEND FORM currencies PROPERTIES(c) READONLY extraSID;

//----------------------------------------------- Импорт курсов ----------------------------------------------------
numCode = DATA LOCAL STRING(INTEGER);
charCode = DATA LOCAL STRING(INTEGER);
nominal = DATA LOCAL STRING(INTEGER);
name = DATA LOCAL STRING(INTEGER);
value = DATA LOCAL STRING(INTEGER);
ID = DATA LOCAL STRING(INTEGER);
date = DATA LOCAL STRING();
name1 = DATA LOCAL STRING();

FORM valCurs FORMEXTID 'ValCurs'
PROPERTIES() date EXTID 'Date' ATTR, name1 EXTID 'name' ATTR

OBJECTS valute = INTEGER EXTID 'Valute'
PROPERTIES(valute) numCode EXTID 'NumCode', charCode EXTID 'CharCode', nominal EXTID 'Nominal', name EXTID 'Name', value EXTID 'Value', ID ATTR
;

importCBRFExchangeRate 'Импортировать курсы ЦБРФ'()  { 
    NEWSESSION {
        LOCAL xml = FILE ();
                
        EXTERNAL HTTP GET 'http://www.cbr.ru/scripts/XML_daily.asp?' TO xml; 
        IMPORT valCurs XML FROM xml();
        
        LOCAL type = TypeExchange();
        type() <- GROUP LAST TypeExchange t IF currency(t) == currencyShortName('RUR') ORDER t;
        
        IF NOT type() THEN {
            MESSAGE 'Необходимо создать тип обмена';
            BREAK;
        } 
        ELSE {
            FOR [GROUP SUM 1 IF (name(Currency c) == name(INTEGER i) OR numCode(i) == extraSID(c) OR shortName(c) == charCode(i)) 
                BY i, c](INTEGER i, Currency c) DO {
                value(i) <- replace(value(i), ',', '.');
                rate(type(), c, toDateFormat(date(), 'dd-MM-yyyy')) <- NUMERIC[20,13](NUMERIC[8,6](value(i)) / INTEGER(nominal(i)));
                name(c) <- STRING[50](name(i));
                extraSID(c) <- BPSTRING[6](numCode(i));
                shortName(c) <- BPSTRING[3](charCode(i));                
            }
            rate(type(), currencyShortName('RUR'), toDateFormat(date(), 'dd-MM-yyyy')) <- 1;
        }
        APPLY;
    } 
}

EXTEND FORM typeExchangeCurrencyDate
    PROPERTIES() importCBRFExchangeRate DRAW t TOOLBAR 
;