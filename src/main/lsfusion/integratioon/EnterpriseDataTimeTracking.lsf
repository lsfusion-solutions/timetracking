MODULE EnterpriseDataTimeTracking;

REQUIRE EnterpriseDataSend, EnterpriseDataReceive, Task, Invoice, EGRUL;

// item
CLASS ItemTimeTracking : Item;
EXTEND CLASS ItemTimeTracking { overheads 'Накладные расходы' }

name (ItemTimeTracking d) += 'Накладные расходы' IF d = ItemTimeTracking.overheads;;

// partner
EXTEND CLASS Partner.Partner : EnterpriseData.Partner;

EnterpriseData.name (Partner.Partner p) += STRING(namePartner(p));
type (Partner.Partner p) += IF p IS Person THEN TypePartner.individual ELSE TypePartner.legalEntity;

// company - hardcode
CLASS Company : EnterpriseData.Company;

EXTEND CLASS Company {
    this
}

name (Company c) += 'ООО "АСБ Консалтинг Групп"' IF c = Company.this;
type (Company c) += TypePartner.legalEntity IF c = Company.this;

// currency - hardcode
CLASS Currency : EnterpriseData.Currency;

EXTEND CLASS Currency {
    this
}

name (Currency c) += 'руб.' IF c = Currency.this;
id (Currency c) += '646' IF c = Currency.this;

// invoice
EXTEND CLASS Invoice : SaleOrder;

company (Invoice i) += Company.this;
currency (Invoice i) += Currency.this;
sum (Invoice i) += NUMERIC[15,2](sumPay(i));
customer (Invoice i) += partner(Invoice.customer(i));
dateTime (Invoice i) += DATETIME(date(i));
id (Invoice i) += CONCAT '-', '00' + prefix(), seriesNumber(i);

// lines of Invoice
EXTEND CLASS Invoice : SaleOrderServiceDetail;
in (Invoice i, Invoice d) += i = d;
item(Invoice i, Invoice d) += ItemTimeTracking.overheads IF d IS Invoice AND i IS Invoice;
quantity (Invoice i, Invoice d) += OVERRIDE NUMERIC[15,3](hoursPay(d) IF i IS Invoice), 0.0;
sum (Invoice i, Invoice d) += OVERRIDE NUMERIC[15,2](sumPay(d) IF i IS Invoice), 0.0;
price (Invoice i, Invoice d) += OVERRIDE NUMERIC[15,2](sum(i, d) / quantity(i, d)) IF quantity(i, d) != 0.0, NUMERIC[15,2](sum(i, d)), 0.0;

EXTEND FORM sendMessage 
    FILTERS customer(saleOrder), sumPay(saleOrder), sum(saleOrder, saleOrderServiceDetail) != 0
;

// receive
afterReceive () + {
    FOR uidPartner(INTEGER i) AND NOT object(uidPartner(i)) AND typePartner(i) = 'ЮридическоеЛицо' DO NEW le = LegalEntity {
        uid(le) <- uidPartner(i);
    }
    FOR uidPartner(INTEGER i) = uid(LegalEntity le) AND typePartner(i) = 'ЮридическоеЛицо' DO {
        inn(le) <- innPartner(i);
        shortName(le) <- ISTRING[250](namePartner(i));
        fullName(le) <- ISTRING[250](fullNamePartner(i));
        kpp(le) <- kppPartner(i);
        ogrn(le) <- nonResidentNumber(i);
    }
    
    FOR uidPartner(INTEGER i) AND NOT object(uidPartner(i)) AND typePartner(i) = 'ФизическоеЛицо' DO NEW p = Person {
        uid(p) <- uidPartner(i);    
    }
    FOR uidPartner(INTEGER i) = uid(Person p) AND typePartner(i) = 'ФизическоеЛицо' DO {
        inn(p) <- innPartner(i);
        lastName(p) <- getWord(namePartner(i), ' ', 1);
        firstName(p) <- getWord(namePartner(i), ' ', 2);
        middleName(p) <- getWord(namePartner(i), ' ', 3);
    }    
    APPLY;
}