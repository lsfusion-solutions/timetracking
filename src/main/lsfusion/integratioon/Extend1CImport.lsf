MODULE Extend1CImport;

REQUIRE EnterpriseDataTimeTracking;

// Такая странная штука для импорта "сбоку" от основной логики

CLASS EntryDetail 'Импортированные проводки';

dateTime 'Дата и время' = DATA DATETIME (EntryDetail);
number 'Номер' = DATA STRING (EntryDetail);
partner 'Контргент' = DATA Partner.Partner (EntryDetail);
namePartner 'Контрагент' (EntryDetail e) = name(partner(e)); 
accountman 'Бухгалтер' = DATA STRING (EntryDetail);
quantityEntries 'Количество проводок' = DATA INTEGER (EntryDetail);

partner (ISTRING[36] s) = OVERRIDE object(s) AS LegalEntity, object(s) AS Person; 

partner = DATA LOCAL ISTRING[36](INTEGER);
accountman = DATA LOCAL STRING(INTEGER);
quantityEntries = DATA LOCAL INTEGER(INTEGER);
dateTime = DATA LOCAL DATETIME (INTEGER);
number = DATA LOCAL STRING(INTEGER);
dateStop = DATA LOCAL DATE();
dateStart = DATA LOCAL DATE();

FORM importedEntries FORMEXTID 'Данные'
PROPERTIES() dateStop EXTID 'КонецПериода' ATTR, dateStart EXTID 'НачалоПериода' ATTR

OBJECTS d = INTEGER EXTID 'Документ'
PROPERTIES(d) partner EXTID 'Контрагент', accountman EXTID 'Ответственный' ATTR, quantityEntries EXTID 'КоличествоПроводок', dateTime EXTID 'Дата' ATTR, number EXTID 'Номер' ATTR;

receivePath () = path() + '7725718426.xml';

receiveEntries 'Получить проводки' () {
    NEWSESSION {
        LOCAL f = XMLFILE();
        TRY {
            READ receivePath() TO f;
            IMPORT importedEntries XML FROM f();
            // Не можем быть уверены в уникальности нумерации, поэтому просто сносим все
            DELETE EntryDetail e WHERE DATE(dateTime(e)) <= dateStop() AND DATE(dateTime(e)) >= dateStart();
            FOR DATE(dateTime(INTEGER i)) <= dateStop() AND DATE(dateTime(i)) >= dateStart() NEW e = EntryDetail DO {
                dateTime(e) <- dateTime(i);
                number(e) <- number(i);
                partner(e) <- partner(partner(i));
                accountman(e) <- accountman(i);
                quantityEntries(e) <- quantityEntries(i);
            }
            APPLY;
            delete(receivePath());
        } CATCH {
            CANCEL;
        }
    }
} TOOLBAR;

FORM entryDashboard 'Импортированные проводки'
    OBJECTS e = EntryDetail
    PROPERTIES (e) dateTime, number, namePartner, quantityEntries
    PROPERTIES () receiveEntries
;

NAVIGATOR {
    dashboard {
        NEW entryDashboard;
    }
}